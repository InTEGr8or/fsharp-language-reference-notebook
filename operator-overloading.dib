#!markdown

# Operator Overloading

This topic describes how to overload arithmetic operators in a class or record type, and at the global level.

## Syntax

#!fsharp

// Overloading an operator as a class or record member.
static member (operator-symbols) (parameter-list) =
    method-body
// Overloading an operator at the global level
let [inline] (operator-symbols) parameter-list = function-body


#!markdown

## Remarks

In the previous syntax, the <em>operator-symbol</em> is one of `+`, `-`, `*`, `/`, `=`, and so on. The <em>parameter-list</em> specifies the operands in the order they appear in the usual syntax for that operator. The <em>method-body</em> constructs the resulting value.

Operator overloads for operators must be static. Operator overloads for unary operators, such as `+` and `-`, must use a tilde (`~`) in the <em>operator-symbol</em> to indicate that the operator is a unary operator and not a binary operator, as shown in the following declaration.

#!fsharp

static member (~-) (v : Vector)


#!markdown

The following code illustrates a vector class that has just two operators, one for unary minus and one for multiplication by a scalar. In the example, two overloads for scalar multiplication are needed because the operator must work regardless of the order in which the vector and scalar appear.

#!fsharp

type Vector(x: float, y : float) =
   member this.x = x
   member this.y = y
   static member (~-) (v : Vector) =
     Vector(-1.0 * v.x, -1.0 * v.y)
   static member (*) (v : Vector, a) =
     Vector(a * v.x, a * v.y)
   static member (*) (a, v: Vector) =
     Vector(a * v.x, a * v.y)
   override this.ToString() =
     this.x.ToString() + " " + this.y.ToString()

let v1 = Vector(1.0, 2.0)

let v2 = v1 * 2.0
let v3 = 2.0 * v1

let v4 = - v2

printfn "%s" (v1.ToString())
printfn "%s" (v2.ToString())
printfn "%s" (v3.ToString())
printfn "%s" (v4.ToString())


#!markdown

## Creating New Operators

You can overload all the standard operators, but you can also create new operators out of sequences of certain characters. Allowed operator characters are `!`, `$`, `%`, `&amp;`, `*`, `+`, `-`, `.`, `/`, `&lt;`, `=`, `&gt;`, `?`, `@`, `^`, `|`, and `~`. The `~` character has the special meaning of making an operator unary, and is not part of the operator character sequence. Not all operators can be made unary.

Depending on the exact character sequence you use, your operator will have a certain precedence and associativity. Associativity can be either left to right or right to left and is used whenever operators of the same level of precedence appear in sequence without parentheses.

The operator character `.` does not affect precedence, so that, for example, if you want to define your own version of multiplication that has the same precedence and associativity as ordinary multiplication, you could create operators such as `.*`.

Only the operators `?` and `?&lt;-` may start with `?`.

The `$` operator must stand alone and without additional symbols.

A table that shows the precedence of all operators in F# can be found in <a href="symbol-and-operator-reference/" data-linktype="relative-path">Symbol and Operator Reference</a>.

## Overloaded Operator Names

When the F# compiler compiles an operator expression, it generates a method that has a compiler-generated name for that operator. This is the name that appears in the Microsoft intermediate language (MSIL) for the method, and also in reflection and IntelliSense. You do not normally need to use these names in F# code.

The following table shows the standard operators and their corresponding generated names.

<table class="table"><caption class="visually-hidden">Overloaded Operator Names</caption>
<thead>
<tr>
<th>Operator</th>
<th>Generated name</th>
</tr>
</thead>
<tbody>
<tr>
<td>`[]`</td>
<td>`op_Nil`</td>
</tr>
<tr>
<td>`::`</td>
<td>`op_Cons`</td>
</tr>
<tr>
<td>`+`</td>
<td>`op_Addition`</td>
</tr>
<tr>
<td>`-`</td>
<td>`op_Subtraction`</td>
</tr>
<tr>
<td>`*`</td>
<td>`op_Multiply`</td>
</tr>
<tr>
<td>`/`</td>
<td>`op_Division`</td>
</tr>
<tr>
<td>`@`</td>
<td>`op_Append`</td>
</tr>
<tr>
<td>`^`</td>
<td>`op_Concatenate`</td>
</tr>
<tr>
<td>`%`</td>
<td>`op_Modulus`</td>
</tr>
<tr>
<td>`&amp;&amp;&amp;`</td>
<td>`op_BitwiseAnd`</td>
</tr>
<tr>
<td>`|||`</td>
<td>`op_BitwiseOr`</td>
</tr>
<tr>
<td>`^^^`</td>
<td>`op_ExclusiveOr`</td>
</tr>
<tr>
<td>`&lt;&lt;&lt;`</td>
<td>`op_LeftShift`</td>
</tr>
<tr>
<td>`~~~`</td>
<td>`op_LogicalNot`</td>
</tr>
<tr>
<td>`&gt;&gt;&gt;`</td>
<td>`op_RightShift`</td>
</tr>
<tr>
<td>`~+`</td>
<td>`op_UnaryPlus`</td>
</tr>
<tr>
<td>`~-`</td>
<td>`op_UnaryNegation`</td>
</tr>
<tr>
<td>`=`</td>
<td>`op_Equality`</td>
</tr>
<tr>
<td>`&lt;=`</td>
<td>`op_LessThanOrEqual`</td>
</tr>
<tr>
<td>`&gt;=`</td>
<td>`op_GreaterThanOrEqual`</td>
</tr>
<tr>
<td>`&lt;`</td>
<td>`op_LessThan`</td>
</tr>
<tr>
<td>`&gt;`</td>
<td>`op_GreaterThan`</td>
</tr>
<tr>
<td>`?`</td>
<td>`op_Dynamic`</td>
</tr>
<tr>
<td>`?&lt;-`</td>
<td>`op_DynamicAssignment`</td>
</tr>
<tr>
<td>`|&gt;`</td>
<td>`op_PipeRight`</td>
</tr>
<tr>
<td>`&lt;|`</td>
<td>`op_PipeLeft`</td>
</tr>
<tr>
<td>`!`</td>
<td>`op_Dereference`</td>
</tr>
<tr>
<td>`&gt;&gt;`</td>
<td>`op_ComposeRight`</td>
</tr>
<tr>
<td>`&lt;&lt;`</td>
<td>`op_ComposeLeft`</td>
</tr>
<tr>
<td>`&lt;@ @&gt;`</td>
<td>`op_Quotation`</td>
</tr>
<tr>
<td>`&lt;@@ @@&gt;`</td>
<td>`op_QuotationUntyped`</td>
</tr>
<tr>
<td>`+=`</td>
<td>`op_AdditionAssignment`</td>
</tr>
<tr>
<td>`-=`</td>
<td>`op_SubtractionAssignment`</td>
</tr>
<tr>
<td>`*=`</td>
<td>`op_MultiplyAssignment`</td>
</tr>
<tr>
<td>`/=`</td>
<td>`op_DivisionAssignment`</td>
</tr>
<tr>
<td>`..`</td>
<td>`op_Range`</td>
</tr>
<tr>
<td>`.. ..`</td>
<td>`op_RangeStep`</td>
</tr>
</tbody>
</table>

Note that the `not` operator in F# does not emit `op_Inequality` because it is not a symbolic operator. It is a function that emits IL that negates a boolean expression.

Other combinations of operator characters that are not listed here can be used as operators and have names that are made up by concatenating names for the individual characters from the following table. For example, +! becomes `op_PlusBang`.

<table class="table"><caption class="visually-hidden">Table 2</caption>
<thead>
<tr>
<th>Operator character</th>
<th>Name</th>
</tr>
</thead>
<tbody>
<tr>
<td>`&gt;`</td>
<td>`Greater`</td>
</tr>
<tr>
<td>`&lt;`</td>
<td>`Less`</td>
</tr>
<tr>
<td>`+`</td>
<td>`Plus`</td>
</tr>
<tr>
<td>`-`</td>
<td>`Minus`</td>
</tr>
<tr>
<td>`*`</td>
<td>`Multiply`</td>
</tr>
<tr>
<td>`/`</td>
<td>`Divide`</td>
</tr>
<tr>
<td>`=`</td>
<td>`Equals`</td>
</tr>
<tr>
<td>`~`</td>
<td>`Twiddle`</td>
</tr>
<tr>
<td>`$`</td>
<td>`Dollar`</td>
</tr>
<tr>
<td>`%`</td>
<td>`Percent`</td>
</tr>
<tr>
<td>`.`</td>
<td>`Dot`</td>
</tr>
<tr>
<td>`&amp;`</td>
<td>`Amp`</td>
</tr>
<tr>
<td>`|`</td>
<td>`Bar`</td>
</tr>
<tr>
<td>`@`</td>
<td>`At`</td>
</tr>
<tr>
<td>`^`</td>
<td>`Hat`</td>
</tr>
<tr>
<td>`!`</td>
<td>`Bang`</td>
</tr>
<tr>
<td>`?`</td>
<td>`Qmark`</td>
</tr>
<tr>
<td>`(`</td>
<td>`LParen`</td>
</tr>
<tr>
<td>`,`</td>
<td>`Comma`</td>
</tr>
<tr>
<td>`)`</td>
<td>`RParen`</td>
</tr>
<tr>
<td>`[`</td>
<td>`LBrack`</td>
</tr>
<tr>
<td>`]`</td>
<td>`RBrack`</td>
</tr>
</tbody>
</table>

## Prefix and Infix Operators

<em>Prefix</em> operators are expected to be placed in front of an operand or operands, much like a function. <em>Infix</em> operators are expected to be placed between the two operands.

Only certain operators can be used as prefix operators. Some operators are always prefix operators, others can be infix or prefix, and the rest are always infix operators. Operators that begin with `!`, except `!=`, and the operator `~`, or repeated sequences of`~`, are always prefix operators. The operators `+`, `-`, `+.`, `-.`, `&amp;`, `&amp;&amp;`, `%`, and `%%` can be prefix operators or infix operators. You distinguish the prefix version of these operators from the infix version by adding a `~` at the beginning of a prefix operator when it is defined. The `~` is not used when you use the operator, only when it is defined.

## Example

The following code illustrates the use of operator overloading to implement a fraction type. A fraction is represented by a numerator and a denominator. The function `hcf` is used to determine the highest common factor, which is used to reduce fractions.

#!fsharp

// Determine the highest common factor between
// two positive integers, a helper for reducing
// fractions.
let rec hcf a b =
  if a = 0u then b
  elif a<b then hcf a (b - a)
  else hcf (a - b) b

// type Fraction: represents a positive fraction
// (positive rational number).
type Fraction =
   {
      // n: Numerator of fraction.
      n : uint32
      // d: Denominator of fraction.
      d : uint32
   }

   // Produce a string representation. If the
   // denominator is "1", do not display it.
   override this.ToString() =
      if (this.d = 1u)
        then this.n.ToString()
        else this.n.ToString() + "/" + this.d.ToString()

   // Add two fractions.
   static member (+) (f1 : Fraction, f2 : Fraction) =
      let nTemp = f1.n * f2.d + f2.n * f1.d
      let dTemp = f1.d * f2.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // Adds a fraction and a positive integer.
   static member (+) (f1: Fraction, i : uint32) =
      let nTemp = f1.n + i * f1.d
      let dTemp = f1.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // Adds a positive integer and a fraction.
   static member (+) (i : uint32, f2: Fraction) =
      let nTemp = f2.n + i * f2.d
      let dTemp = f2.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // Subtract one fraction from another.
   static member (-) (f1 : Fraction, f2 : Fraction) =
      if (f2.n * f1.d > f1.n * f2.d)
        then failwith "This operation results in a negative number, which is not supported."
      let nTemp = f1.n * f2.d - f2.n * f1.d
      let dTemp = f1.d * f2.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // Multiply two fractions.
   static member (*) (f1 : Fraction, f2 : Fraction) =
      let nTemp = f1.n * f2.n
      let dTemp = f1.d * f2.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // Divide two fractions.
   static member (/) (f1 : Fraction, f2 : Fraction) =
      let nTemp = f1.n * f2.d
      let dTemp = f2.n * f1.d
      let hcfTemp = hcf nTemp dTemp
      { n = nTemp / hcfTemp; d = dTemp / hcfTemp }

   // A full set of operators can be quite lengthy. For example,
   // consider operators that support other integral data types,
   // with fractions, on the left side and the right side for each.
   // Also consider implementing unary operators.

let fraction1 = { n = 3u; d = 4u }
let fraction2 = { n = 1u; d = 2u }
let result1 = fraction1 + fraction2
let result2 = fraction1 - fraction2
let result3 = fraction1 * fraction2
let result4 = fraction1 / fraction2
let result5 = fraction1 + 1u
printfn "%s + %s = %s" (fraction1.ToString()) (fraction2.ToString()) (result1.ToString())
printfn "%s - %s = %s" (fraction1.ToString()) (fraction2.ToString()) (result2.ToString())
printfn "%s * %s = %s" (fraction1.ToString()) (fraction2.ToString()) (result3.ToString())
printfn "%s / %s = %s" (fraction1.ToString()) (fraction2.ToString()) (result4.ToString())
printfn "%s + 1 = %s" (fraction1.ToString()) (result5.ToString())


#!markdown

<strong>Output:</strong>

#!fsharp

3/4 + 1/2 = 5/4
3/4 - 1/2 = 1/4
3/4 * 1/2 = 3/8
3/4 / 1/2 = 3/2
3/4 + 1 = 7/4


#!markdown

## Operators at the Global Level

You can also define operators at the global level. The following code defines an operator `+?`.

#!fsharp

let inline (+?) (x: int) (y: int) = x + 2*y
printf "%d" (10 +? 1)


#!markdown

The output of the above code is `12`.

You can redefine the regular arithmetic operators in this manner because the scoping rules for F# dictate that newly defined operators take precedence over the built-in operators.

The keyword `inline` is often used with global operators, which are often small functions that are best integrated into the calling code. Making operator functions inline also enables them to work with statically resolved type parameters to produce statically resolved generic code. For more information, see <a href="functions/inline-functions" data-linktype="relative-path">Inline Functions</a> and <a href="generics/statically-resolved-type-parameters" data-linktype="relative-path">Statically Resolved Type Parameters</a>.

## See also

