#!markdown

# Computation Expressions

Computation expressions in F# provide a convenient syntax for writing computations that can be sequenced and combined using control flow constructs and bindings. Depending on the kind of computation expression, they can be thought of as a way to express monads, monoids, monad transformers, and applicative functors. However, unlike other languages (such as <em>do-notation</em> in Haskell), they are not tied to a single abstraction, and do not rely on macros or other forms of metaprogramming to accomplish a convenient and context-sensitive syntax.

## Overview

Computations can take many forms. The most common form of computation is single-threaded execution, which is easy to understand and modify. However, not all forms of computation are as straightforward as single-threaded execution. Some examples include:

More generally, there are <em>context-sensitive</em> computations that you must perform in certain parts of an application. Writing context-sensitive code can be challenging, as it is easy to "leak" computations outside of a given context without abstractions to prevent you from doing so. These abstractions are often challenging to write by yourself, which is why F# has a generalized way to do so called <strong>computation expressions</strong>.

Computation expressions offer a uniform syntax and abstraction model for encoding context-sensitive computations.

Every computation expression is backed by a <em>builder</em> type. The builder type defines the operations that are available for the computation expression. See <a href="computation-expressions#creating-a-new-type-of-computation-expression" data-linktype="relative-path">Creating a New Type of Computation Expression</a>, which shows how to create a custom computation expression.

All computation expressions have the following form:

#!fsharp

builder-expr { cexper }


#!markdown

where `builder-expr` is the name of a builder type that defines the computation expression, and `cexper` is the expression body of the computation expression. For example, `async` computation expression code can look like this:

#!fsharp

let fetchAndDownload url =
    async {
        let! data = downloadData url

        let processedData = processData data

        return processedData
    }


#!markdown

There is a special, additional syntax available within a computation expression, as shown in the previous example. The following expression forms are possible with computation expressions:

#!fsharp

expr { let! ... }
expr { do! ... }
expr { yield ... }
expr { yield! ... }
expr { return ... }
expr { return! ... }
expr { match! ... }


#!markdown

Each of these keywords, and other standard F# keywords are only available in a computation expression if they have been defined in the backing builder type. The only exception to this is `match!`, which is itself syntactic sugar for the use of `let!` followed by a pattern match on the result.

The builder type is an object that defines special methods that govern the way the fragments of the computation expression are combined; that is, its methods control how the computation expression behaves. Another way to describe a builder class is to say that it enables you to customize the operation of many F# constructs, such as loops and bindings.

The `let!` keyword binds the result of a call to another computation expression to a name:

#!fsharp

let doThingsAsync url =
    async {
        let! data = getDataAsync url
        ...
    }


#!markdown

If you bind the call to a computation expression with `let`, you will not get the result of the computation expression. Instead, you will have bound the value of the <em>unrealized</em> call to that computation expression. Use `let!` to bind to the result.

`let!` is defined by the `Bind(x, f)` member on the builder type.

The `do!` keyword is for calling a computation expression that returns a `unit`-like type (defined by the `Zero` member on the builder):

#!fsharp

let doThingsAsync data url =
    async {
        do! submitData data url
        ...
    }


#!markdown

For the <a href="asynchronous-workflows" data-linktype="relative-path">async workflow</a>, this type is `Async&lt;unit&gt;`. For other computation expressions, the type is likely to be `CExpType&lt;unit&gt;`.

`do!` is defined by the `Bind(x, f)` member on the builder type, where `f` produces a `unit`.

The `yield` keyword is for returning a value from the computation expression so that it can be consumed as an <a href="/en-us/dotnet/api/system.collections.generic.ienumerable-1" data-linktype="absolute-path">IEnumerable&lt;T&gt;</a>:

#!fsharp

let squares =
    seq {
        for i in 1..10 do
            yield i * i
    }

for sq in squares do
    printfn $"%d{sq}"


#!markdown

In most cases, it can be omitted by callers. The most common way to omit `yield` is with the `-&gt;` operator:

#!fsharp

let squares =
    seq {
        for i in 1..10 -> i * i
    }

for sq in squares do
    printfn $"%d{sq}"


#!markdown

For more complex expressions that might yield many different values, and perhaps conditionally, simply omitting the keyword can do:

#!fsharp

let weekdays includeWeekend =
    seq {
        "Monday"
        "Tuesday"
        "Wednesday"
        "Thursday"
        "Friday"
        if includeWeekend then
            "Saturday"
            "Sunday"
    }


#!markdown

As with the <a href="../../csharp/language-reference/keywords/yield" data-linktype="relative-path">yield keyword in C#</a>, each element in the computation expression is yielded back as it is iterated.

`yield` is defined by the `Yield(x)` member on the builder type, where `x` is the item to yield back.

The `yield!` keyword is for flattening a collection of values from a computation expression:

#!fsharp

let squares =
    seq {
        for i in 1..3 -> i * i
    }

let cubes =
    seq {
        for i in 1..3 -> i * i * i
    }

let squaresAndCubes =
    seq {
        yield! squares
        yield! cubes
    }

printfn $"{squaresAndCubes}"  // Prints - 1; 4; 9; 1; 8; 27


#!markdown

When evaluated, the computation expression called by `yield!` will have its items yielded back one-by-one, flattening the result.

`yield!` is defined by the `YieldFrom(x)` member on the builder type, where `x` is a collection of values.

Unlike `yield`, `yield!` must be explicitly specified. Its behavior isn't implicit in computation expressions.

The `return` keyword wraps a value in the type corresponding to the computation expression. Aside from computation expressions using `yield`, it is used to "complete" a computation expression:

#!fsharp

let req = // 'req' is of type 'Async<data>'
    async {
        let! data = fetch url
        return data
    }

// 'result' is of type 'data'
let result = Async.RunSynchronously req


#!markdown

`return` is defined by the `Return(x)` member on the builder type, where `x` is the item to wrap.

The `return!` keyword realizes the value of a computation expression and wraps that result in the type corresponding to the computation expression:

#!fsharp

let req = // 'req' is of type 'Async<data>'
    async {
        return! fetch url
    }

// 'result' is of type 'data'
let result = Async.RunSynchronously req


#!markdown

`return!` is defined by the `ReturnFrom(x)` member on the builder type, where `x` is another computation expression.

The `match!` keyword allows you to inline a call to another computation expression and pattern match on its result:

#!fsharp

let doThingsAsync url =
    async {
        match! callService url with
        | Some data -> ...
        | None -> ...
    }


#!markdown

When calling a computation expression with `match!`, it will realize the result of the call like `let!`. This is often used when calling a computation expression where the result is an <a href="options" data-linktype="relative-path">optional</a>.

## Built-in computation expressions

The F# core library defines three built-in computation expressions: <a href="sequences" data-linktype="relative-path">Sequence Expressions</a>, <a href="asynchronous-workflows" data-linktype="relative-path">Asynchronous Workflows</a>, and <a href="query-expressions" data-linktype="relative-path">Query Expressions</a>.

## Creating a New Type of Computation Expression

You can define the characteristics of your own computation expressions by creating a builder class and defining certain special methods on the class. The builder class can optionally define the methods as listed in the following table.

The following table describes methods that can be used in a workflow builder class.

<table class="table"><caption class="visually-hidden">Creating a New Type of Computation Expression</caption>
<thead>
<tr>
<th><strong>Method</strong></th>
<th><strong>Typical signature(s)</strong></th>
<th><strong>Description</strong></th>
</tr>
</thead>
<tbody>
<tr>
<td>`Bind`</td>
<td>`M&lt;'T&gt; * ('T -&gt; M&lt;'U&gt;) -&gt; M&lt;'U&gt;`</td>
<td>Called for `let!` and `do!` in computation expressions.</td>
</tr>
<tr>
<td>`Delay`</td>
<td>`(unit -&gt; M&lt;'T&gt;) -&gt; M&lt;'T&gt;`</td>
<td>Wraps a computation expression as a function.</td>
</tr>
<tr>
<td>`Return`</td>
<td>`'T -&gt; M&lt;'T&gt;`</td>
<td>Called for `return` in computation expressions.</td>
</tr>
<tr>
<td>`ReturnFrom`</td>
<td>`M&lt;'T&gt; -&gt; M&lt;'T&gt;`</td>
<td>Called for `return!` in computation expressions.</td>
</tr>
<tr>
<td>`Run`</td>
<td>`M&lt;'T&gt; -&gt; M&lt;'T&gt;` or<br><br>`M&lt;'T&gt; -&gt; 'T`</td>
<td>Executes a computation expression.</td>
</tr>
<tr>
<td>`Combine`</td>
<td>`M&lt;'T&gt; * M&lt;'T&gt; -&gt; M&lt;'T&gt;` or<br><br>`M&lt;unit&gt; * M&lt;'T&gt; -&gt; M&lt;'T&gt;`</td>
<td>Called for sequencing in computation expressions.</td>
</tr>
<tr>
<td>`For`</td>
<td>`seq&lt;'T&gt; * ('T -&gt; M&lt;'U&gt;) -&gt; M&lt;'U&gt;` or<br><br>`seq&lt;'T&gt; * ('T -&gt; M&lt;'U&gt;) -&gt; seq&lt;M&lt;'U&gt;&gt;`</td>
<td>Called for `for...do` expressions in computation expressions.</td>
</tr>
<tr>
<td>`TryFinally`</td>
<td>`M&lt;'T&gt; * (unit -&gt; unit) -&gt; M&lt;'T&gt;`</td>
<td>Called for `try...finally` expressions in computation expressions.</td>
</tr>
<tr>
<td>`TryWith`</td>
<td>`M&lt;'T&gt; * (exn -&gt; M&lt;'T&gt;) -&gt; M&lt;'T&gt;`</td>
<td>Called for `try...with` expressions in computation expressions.</td>
</tr>
<tr>
<td>`Using`</td>
<td>`'T * ('T -&gt; M&lt;'U&gt;) -&gt; M&lt;'U&gt; when 'T :&gt; IDisposable`</td>
<td>Called for `use` bindings in computation expressions.</td>
</tr>
<tr>
<td>`While`</td>
<td>`(unit -&gt; bool) * M&lt;'T&gt; -&gt; M&lt;'T&gt;`</td>
<td>Called for `while...do` expressions in computation expressions.</td>
</tr>
<tr>
<td>`Yield`</td>
<td>`'T -&gt; M&lt;'T&gt;`</td>
<td>Called for `yield` expressions in computation expressions.</td>
</tr>
<tr>
<td>`YieldFrom`</td>
<td>`M&lt;'T&gt; -&gt; M&lt;'T&gt;`</td>
<td>Called for `yield!` expressions in computation expressions.</td>
</tr>
<tr>
<td>`Zero`</td>
<td>`unit -&gt; M&lt;'T&gt;`</td>
<td>Called for empty `else` branches of `if...then` expressions in computation expressions.</td>
</tr>
<tr>
<td>`Quote`</td>
<td>`Quotations.Expr&lt;'T&gt; -&gt; Quotations.Expr&lt;'T&gt;`</td>
<td>Indicates that the computation expression is passed to the `Run` member as a quotation. It translates all instances of a computation into a quotation.</td>
</tr>
</tbody>
</table>

Many of the methods in a builder class use and return an `M&lt;'T&gt;` construct, which is typically a separately defined type that characterizes the kind of computations being combined, for example, `Async&lt;'T&gt;` for asynchronous workflows and `Seq&lt;'T&gt;` for sequence workflows. The signatures of these methods enable them to be combined and nested with each other, so that the workflow object returned from one construct can be passed to the next. The compiler, when it parses a computation expression, converts the expression into a series of nested function calls by using the methods in the preceding table and the code in the computation expression.

The nested expression is of the following form:

#!fsharp

builder.Run(builder.Delay(fun () -> {| cexpr |}))


#!markdown

In the above code, the calls to `Run` and `Delay` are omitted if they are not defined in the computation expression builder class. The body of the computation expression, here denoted as `{| cexpr |}`, is translated into calls involving the methods of the builder class by the translations described in the following table. The computation expression `{| cexpr |}` is defined recursively according to these translations where `expr` is an F# expression and `cexpr` is a computation expression.

<table class="table"><caption class="visually-hidden">Table 2</caption>
<thead>
<tr>
<th>Expression</th>
<th>Translation</th>
</tr>
</thead>
<tbody>
<tr>
<td>`{ let binding in cexpr }`</td>
<td>`let binding in {| cexpr |}`</td>
</tr>
<tr>
<td>`{ let! pattern = expr in cexpr }`</td>
<td>`builder.Bind(expr, (fun pattern -&gt; {| cexpr |}))`</td>
</tr>
<tr>
<td>`{ do! expr in cexpr }`</td>
<td>`builder.Bind(expr, (fun () -&gt; {| cexpr |}))`</td>
</tr>
<tr>
<td>`{ yield expr }`</td>
<td>`builder.Yield(expr)`</td>
</tr>
<tr>
<td>`{ yield! expr }`</td>
<td>`builder.YieldFrom(expr)`</td>
</tr>
<tr>
<td>`{ return expr }`</td>
<td>`builder.Return(expr)`</td>
</tr>
<tr>
<td>`{ return! expr }`</td>
<td>`builder.ReturnFrom(expr)`</td>
</tr>
<tr>
<td>`{ use pattern = expr in cexpr }`</td>
<td>`builder.Using(expr, (fun pattern -&gt; {| cexpr |}))`</td>
</tr>
<tr>
<td>`{ use! value = expr in cexpr }`</td>
<td>`builder.Bind(expr, (fun value -&gt; builder.Using(value, (fun value -&gt; { cexpr }))))`</td>
</tr>
<tr>
<td>`{ if expr then cexpr0 |}`</td>
<td>`if expr then { cexpr0 } else builder.Zero()`</td>
</tr>
<tr>
<td>`{ if expr then cexpr0 else cexpr1 |}`</td>
<td>`if expr then { cexpr0 } else { cexpr1 }`</td>
</tr>
<tr>
<td>`{ match expr with | pattern_i -&gt; cexpr_i }`</td>
<td>`match expr with | pattern_i -&gt; { cexpr_i }`</td>
</tr>
<tr>
<td>`{ for pattern in expr do cexpr }`</td>
<td>`builder.For(enumeration, (fun pattern -&gt; { cexpr }))`</td>
</tr>
<tr>
<td>`{ for identifier = expr1 to expr2 do cexpr }`</td>
<td>`builder.For(enumeration, (fun identifier -&gt; { cexpr }))`</td>
</tr>
<tr>
<td>`{ while expr do cexpr }`</td>
<td>`builder.While(fun () -&gt; expr, builder.Delay({ cexpr }))`</td>
</tr>
<tr>
<td>`{ try cexpr with | pattern_i -&gt; expr_i }`</td>
<td>`builder.TryWith(builder.Delay({ cexpr }), (fun value -&gt; match value with | pattern_i -&gt; expr_i | exn -&gt; reraise exn)))`</td>
</tr>
<tr>
<td>`{ try cexpr finally expr }`</td>
<td>`builder.TryFinally(builder.Delay( { cexpr }), (fun () -&gt; expr))`</td>
</tr>
<tr>
<td>`{ cexpr1; cexpr2 }`</td>
<td>`builder.Combine({ cexpr1 }, { cexpr2 })`</td>
</tr>
<tr>
<td>`{ other-expr; cexpr }`</td>
<td>`expr; { cexpr }`</td>
</tr>
<tr>
<td>`{ other-expr }`</td>
<td>`expr; builder.Zero()`</td>
</tr>
</tbody>
</table>

In the previous table, `other-expr` describes an expression that is not otherwise listed in the table. A builder class does not need to implement all of the methods and support all of the translations listed in the previous table. Those constructs that are not implemented are not available in computation expressions of that type. For example, if you do not want to support the `use` keyword in your computation expressions, you can omit the definition of `Use` in your builder class.

The following code example shows a computation expression that encapsulates a computation as a series of steps that can be evaluated one step at a time. A discriminated union type, `OkOrException`, encodes the error state of the expression as evaluated so far. This code demonstrates several typical patterns that you can use in your computation expressions, such as boilerplate implementations of some of the builder methods.

#!fsharp

// Computations that can be run step by step
type Eventually<'T> =
    | Done of 'T
    | NotYetDone of (unit -> Eventually<'T>)

module Eventually =
    // The bind for the computations. Append 'func' to the
    // computation.
    let rec bind func expr =
        match expr with
        | Done value -> func value
        | NotYetDone work -> NotYetDone (fun () -> bind func (work()))

    // Return the final value wrapped in the Eventually type.
    let result value = Done value

    type OkOrException<'T> =
        | Ok of 'T
        | Exception of System.Exception

    // The catch for the computations. Stitch try/with throughout
    // the computation, and return the overall result as an OkOrException.
    let rec catch expr =
        match expr with
        | Done value -> result (Ok value)
        | NotYetDone work ->
            NotYetDone (fun () ->
                let res = try Ok(work()) with | exn -> Exception exn
                match res with
                | Ok cont -> catch cont // note, a tailcall
                | Exception exn -> result (Exception exn))

    // The delay operator.
    let delay func = NotYetDone (fun () -> func())

    // The stepping action for the computations.
    let step expr =
        match expr with
        | Done _ -> expr
        | NotYetDone func -> func ()

    // The rest of the operations are boilerplate.
    // The tryFinally operator.
    // This is boilerplate in terms of "result", "catch", and "bind".
    let tryFinally expr compensation =
        catch (expr)
        |> bind (fun res ->
            compensation();
            match res with
            | Ok value -> result value
            | Exception exn -> raise exn)

    // The tryWith operator.
    // This is boilerplate in terms of "result", "catch", and "bind".
    let tryWith exn handler =
        catch exn
        |> bind (function Ok value -> result value | Exception exn -> handler exn)

    // The whileLoop operator.
    // This is boilerplate in terms of "result" and "bind".
    let rec whileLoop pred body =
        if pred() then body |> bind (fun _ -> whileLoop pred body)
        else result ()

    // The sequential composition operator.
    // This is boilerplate in terms of "result" and "bind".
    let combine expr1 expr2 =
        expr1 |> bind (fun () -> expr2)

    // The using operator.
    let using (resource: #System.IDisposable) func =
        tryFinally (func resource) (fun () -> resource.Dispose())

    // The forLoop operator.
    // This is boilerplate in terms of "catch", "result", and "bind".
    let forLoop (collection:seq<_>) func =
        let ie = collection.GetEnumerator()
        tryFinally
            (whileLoop
                (fun () -> ie.MoveNext())
                (delay (fun () -> let value = ie.Current in func value)))
            (fun () -> ie.Dispose())

// The builder class.
type EventuallyBuilder() =
    member x.Bind(comp, func) = Eventually.bind func comp
    member x.Return(value) = Eventually.result value
    member x.ReturnFrom(value) = value
    member x.Combine(expr1, expr2) = Eventually.combine expr1 expr2
    member x.Delay(func) = Eventually.delay func
    member x.Zero() = Eventually.result ()
    member x.TryWith(expr, handler) = Eventually.tryWith expr handler
    member x.TryFinally(expr, compensation) = Eventually.tryFinally expr compensation
    member x.For(coll:seq<_>, func) = Eventually.forLoop coll func
    member x.Using(resource, expr) = Eventually.using resource expr

let eventually = new EventuallyBuilder()

let comp = eventually {
    for x in 1..2 do
        printfn $" x = %d{x}"
    return 3 + 4 }

// Try the remaining lines in F# interactive to see how this
// computation expression works in practice.
let step x = Eventually.step x

// returns "NotYetDone <closure>"
comp |> step

// prints "x = 1"
// returns "NotYetDone <closure>"
comp |> step |> step

// prints "x = 1"
// prints "x = 2"
// returns "Done 7"
comp |> step |> step |> step |> step


#!markdown

A computation expression has an underlying type, which the expression returns. The underlying type may represent a computed result or a delayed computation that can be performed, or it may provide a way to iterate through some type of collection. In the previous example, the underlying type was <strong>Eventually</strong>. For a sequence expression, the underlying type is <a href="/en-us/dotnet/api/system.collections.generic.ienumerable-1" data-linktype="absolute-path">System.Collections.Generic.IEnumerable&lt;T&gt;</a>. For a query expression, the underlying type is <a href="/en-us/dotnet/api/system.linq.iqueryable" data-linktype="absolute-path">System.Linq.IQueryable</a>. For an asynchronous workflow, the underlying type is <a href="https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-fsharpasync-1.html" data-linktype="external">`Async`</a>. The `Async` object represents the work to be performed to compute the result. For example, you call <a href="https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-fsharpasync.html#RunSynchronously" data-linktype="external">`Async.RunSynchronously`</a> to execute a computation and return the result.

## Custom Operations

You can define a custom operation on a computation expression and use a custom operation as an operator in a computation expression. For example, you can include a query operator in a query expression. When you define a custom operation, you must define the Yield and For methods in the computation expression. To define a custom operation, put it in a builder class for the computation expression, and then apply the <a href="https://fsharp.github.io/fsharp-core-docs/reference/fsharp-core-customoperationattribute.html" data-linktype="external">`CustomOperationAttribute`</a>. This attribute takes a string as an argument, which is the name to be used in a custom operation. This name comes into scope at the start of the opening curly brace of the computation expression. Therefore, you shouldn't use identifiers that have the same name as a custom operation in this block. For example, avoid the use of identifiers such as `all` or `last` in query expressions.

If you already have a builder class, its custom operations can be extended from outside of this builder class. Extensions must be declared in modules. Namespaces cannot contain extension members except in the same file and the same namespace declaration group where the type is defined.

The following example shows the extension of the existing `FSharp.Linq.QueryBuilder` class.

#!fsharp

type FSharp.Linq.QueryBuilder with

    [<CustomOperation("existsNot")>]
    member _.ExistsNot (source: QuerySource<'T, 'Q>, predicate) =
        Enumerable.Any (source.Source, Func<_,_>(predicate)) |> not


#!markdown

## See also

