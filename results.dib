#!markdown

# Results

The `Result&lt;'T,'TFailure&gt;` type lets you write error-tolerant code that can be composed.

## Syntax

#!fsharp

// The definition of Result in FSharp.Core
[<StructuralEquality; StructuralComparison>]
[<CompiledName("FSharpResult`2")>]
[<Struct>]
type Result<'T,'TError> =
    | Ok of ResultValue:'T
    | Error of ErrorValue:'TError


#!markdown

## Remarks

See the <a href="https://fsharp.github.io/fsharp-core-docs/reference/fsharp-core-resultmodule.html" data-linktype="external">`Result`</a> module for the built-in combinators for the `Result`. type.

Note that the result type is a <a href="discriminated-unions#struct-discriminated-unions" data-linktype="relative-path">struct discriminated union</a>. Structural equality semantics apply here.

The `Result` type is typically used in monadic error-handling, which is often referred to as <a href="https://swlaschin.gitbooks.io/fsharpforfunandprofit/content/posts/recipe-part2.html" data-linktype="external">Railway-oriented Programming</a> within the F# community.  The following trivial example demonstrates this approach.

#!fsharp

// Define a simple type which has fields that can be validated
type Request =
    { Name: string
      Email: string }

// Define some logic for what defines a valid name.
//
// Generates a Result which is an Ok if the name validates;
// otherwise, it generates a Result which is an Error.
let validateName req =
    match req.Name with
    | null -> Error "No name found."
    | "" -> Error "Name is empty."
    | "bananas" -> Error "Bananas is not a name."
    | _ -> Ok req

// Similarly, define some email validation logic.
let validateEmail req =
    match req.Email with
    | null -> Error "No email found."
    | "" -> Error "Email is empty."
    | s when s.EndsWith("bananas.com") -> Error "No email from bananas.com is allowed."
    | _ -> Ok req

let validateRequest reqResult =
    reqResult
    |> Result.bind validateName
    |> Result.bind validateEmail

let test() =
    // Now, create a Request and pattern match on the result.
    let req1 = { Name = "Phillip"; Email = "phillip@contoso.biz" }
    let res1 = validateRequest (Ok req1)
    match res1 with
    | Ok req -> printfn $"My request was valid! Name: {req.Name} Email {req.Email}"  
    | Error e -> printfn $"Error: {e}"
    // Prints: "My request was valid!  Name: Phillip Email: phillip@consoto.biz"

    let req2 = { Name = "Phillip"; Email = "phillip@bananas.com" }
    let res2 = validateRequest (Ok req2)
    match res2 with
    | Ok req -> printfn $"My request was valid! Name: {req.Name} Email {req.Email}"  
    | Error e -> printfn $"Error: {e}"
    // Prints: "Error: No email from bananas.com is allowed."

test()


#!markdown

As you can see, it's quite easy to chain together various validation functions if you force them all to return a `Result`.  This lets you break up functionality like this into small pieces which are as composable as you need them to be.  This also has the added value of <em>enforcing</em> the use of <a href="pattern-matching" data-linktype="relative-path">pattern matching</a> at the end of a round of validation, which in turns enforces a higher degree of program correctness.

## See also

